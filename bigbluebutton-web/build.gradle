buildscript {
    repositories {
        mavenLocal()
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"
        classpath "gradle.plugin.com.github.erdi.webdriver-binaries:webdriver-binaries-gradle-plugin:2.0"
        classpath "org.grails.plugins:hibernate5:7.0.4"
        classpath "com.bertramlabs.plugins:asset-pipeline-gradle:3.2.4"
    }
}

version "0.11.0"
group "org.bigbluebutton.web"

apply plugin:"eclipse"
apply plugin:"idea"
apply plugin:"war"
apply plugin:"org.grails.grails-web"
apply plugin:"com.github.erdi.webdriver-binaries"
apply plugin:"com.bertramlabs.asset-pipeline"
apply plugin:"org.grails.grails-gsp"

repositories {
    jcenter()
    mavenLocal()
    maven { url "https://repo.grails.org/grails/core" }
}

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
    compile.exclude group: "org.red5", module: "red5-server-common"
    compile.exclude group: "org.codehaus.groovy", module: "groovy-all"
}

dependencies {
    developmentOnly("org.springframework.boot:spring-boot-devtools")
    compile "org.springframework.boot:spring-boot-starter-logging"
    compile "org.springframework.boot:spring-boot-autoconfigure"
    compile "org.grails:grails-core"
    compile "org.springframework.boot:spring-boot-starter-actuator"
    compile "org.springframework.boot:spring-boot-starter-tomcat"
    compile "org.grails:grails-web-boot"
    compile "org.grails:grails-logging"
    compile "org.grails:grails-plugin-rest"
    compile "org.grails:grails-plugin-databinding"
    compile "org.grails:grails-plugin-i18n"
    compile "org.grails:grails-plugin-services"
    compile "org.grails:grails-plugin-url-mappings"
    compile "org.grails:grails-plugin-interceptors"
    compile "org.grails.plugins:cache"
    compile "org.grails.plugins:async"
    compile "org.grails.plugins:scaffolding"
    compile "org.grails.plugins:events"
    compile "org.grails.plugins:hibernate5"
    compile "org.hibernate:hibernate-core:5.4.18.Final"
    compile "org.grails.plugins:gsp"
    compileOnly "io.micronaut:micronaut-inject-groovy"


    runtime("org.springframework.boot:spring-boot-properties-migrator")


    //--- BigBlueButton Dependencies Start - Transitive dependencies have to be re-defined below
    compile "org.bigbluebutton:bbb-common-message_2.13:0.0.21-SNAPSHOT"
    compile "org.bigbluebutton:bbb-common-web:0.0.4-SNAPSHOT"
    compile "io.lettuce:lettuce-core:6.0.2.RELEASE"
    compile "org.reactivestreams:reactive-streams:1.0.3"
    compile "io.projectreactor:reactor-core:3.4.2"
    compile "org.freemarker:freemarker:2.3.30"
    compile "com.google.code.gson:gson:2.8.6"
    compile "org.json:json:20201115"
    compile "org.jodconverter:jodconverter-local:4.3.0"
    compile "com.zaxxer:nuprocess:1.2.6"
    compile "net.java.dev.jna:jna:5.6.0"
    compile "commons-fileupload:commons-fileupload:1.4"

    console "org.grails:grails-console"
    profile "org.grails.profiles:web"
    runtime "org.glassfish.web:el-impl:2.1.2-b03"
    runtime "org.apache.tomcat:tomcat-jdbc"
    runtime "javax.xml.bind:jaxb-api:2.3.1"
    runtime "com.bertramlabs.plugins:asset-pipeline-grails:3.2.4"}

bootRun {
    ignoreExitValue true
    jvmArgs(
        '-Dspring.output.ansi.enabled=always', 
        '-noverify', 
        '-XX:TieredStopAtLevel=1',
        '-Xmx1024m')
    sourceResources sourceSets.main
    String springProfilesActive = 'spring.profiles.active'
    systemProperty springProfilesActive, System.getProperty(springProfilesActive)
}

task resolveDeps(type: Copy) {
  into('lib')
  from configurations.default
  from configurations.default.allArtifacts.file
  doLast { println '[bbb:task] Resolved dependencies for bigbluebutton-web application' }
}

// Task that creates the index file which lists all extra libs
task copyWebInf(type: Copy) {
  into("${project.buildDir}/resources/main/")
  from("src/main/webapp")
}

// make  processResources depends on copyWebInf task
processResources.dependsOn copyWebInf

tasks.withType(GroovyCompile) {
    configure(groovyOptions) {
        forkOptions.jvmArgs = ['-Xmx1024m']
    }
}

assets {
    minifyJs = true
    minifyCss = true
}
